plugins {
    id 'com.github.hierynomus.license' version '0.13.1'
    id 'com.gradle.plugin-publish' version '0.9.4'
}

// FIXME: add in test coverage and groovydoc

group = 'com.stehno.gradle'
version = '0.2.0'

apply plugin: 'java-gradle-plugin'
apply plugin: 'groovy'

sourceCompatibility = 8
targetCompatibility = 8

repositories {
    jcenter()
}

sourceSets {
    functionalTest {
        groovy {
            srcDir file('src/functionalTest/groovy')
        }
        resources {
            srcDir file('src/functionalTest/resources')
        }
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

task functionalTest(type: Test) {
    testClassesDir = sourceSets.functionalTest.output.classesDir
    classpath = sourceSets.functionalTest.runtimeClasspath
}

check.dependsOn functionalTest

gradlePlugin {
    testSourceSets sourceSets.functionalTest
}

dependencies {
    localGroovy()

    testCompile 'junit:junit:4.12'
    testCompile ('org.spockframework:spock-core:1.0-groovy-2.4'){
        exclude module:'groovy-all'
    }
    testCompile('org.mock-server:mockserver-client-java:3.10.4') {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
    testCompile('org.mock-server:mockserver-netty:3.10.4') {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }

    functionalTestCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    functionalTestCompile('org.mock-server:mockserver-client-java:3.10.4') {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
    functionalTestCompile('org.mock-server:mockserver-netty:3.10.4') {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
}

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join('\n')
    }
}

// Add the classpath file to the test runtime classpath
dependencies {
    testRuntime files(createClasspathManifest)
}

license {
    header rootProject.file('license_header.txt')
    ext.name = 'Christopher J. Stehno'
    ext.email = 'chris@stehno.com'
    ext.year = Calendar.instance.get(Calendar.YEAR)
}

pluginBundle {
    website = 'http://cjstehno.github.io/dependency-checker/'
    vcsUrl = 'https://github.com/cjstehno/dependency-checker'
    description = 'Gradle plugin to aid in managing dependencies.'
    tags = ['gradle', 'groovy']

    plugins {
        dependencyCheckerPlugin {
            id = 'com.stehno.gradle.dependency-checker'
            displayName = 'Gradle Dependency Checker Plugin'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}
